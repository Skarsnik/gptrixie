use XML;
use GPT::Class;
use GPT::DumbGenerator;
use GPT::FileGenerator;
use GPT::FileFilter;
use GPT::HandleFileDeps;
use GPT::ListTypes;
use GPT::ListFiles;
#use OOGenerator;

my $GCC_XML = %*ENV<GPT_GCCXML> || 'gccxml';
my $CASTXML_STD = 'c89';
my $CASTXML = False;
my $SILENT = False;

my $gmerge-stypedef;

sub gpt-note (*@msg) {
   note |@msg unless $SILENT;
}

sub MAIN(
          $header-file #= The header file
         , Bool :$all #= Generate everything
         , Str :$define-enum #= Try to generate enumeration from #define using the given starting pattern
         , Str :$ooc #= Do nothing
         , Bool :$silent = False #= Remove all output on stderr
         , Bool :$enums #= Generate enumerations
         , Bool :$functions #= Generate functions
         , Bool :$structs #= Generate structures and unions
         , Bool :$externs #= Generate extern declaration
         , Bool :$list-types #= Mostly for debug purpose, list all the C type found
         , Bool :$list-files #= List all the files involved
         , Str  :$list-deps #= List the dependancy from other files for this file, based on type used
         , Str :$files #= WIP Allow you to pick from which files you want to generate stuff. eg --files=myheader.h,mysubheader.h.
                       #=
                       #= You can also use file 'id' given by --list-files like   @f1,@f2
                       #=
                       #= You can also exclude file by putting - in front of the file
         , Bool :$merge-stypedef #= Merge a typedef pointing to a struct type to the struct name
         , Str  :$gptfile #= Use the given GPT file to generate a module, all other (gpt) options are ignored
         , Str :$castxml #= allow for gptrixie to use castxml, you need to specificy the C standard
         , *@gccoptions #= remaining options are passed to gccxml. eg -I /path/needed/by/header
         ) {
  if $define-enum.defined and ! $define-enum.index(':').defined {
      die "The define-enum option must be of the form enumname:pattern";
  }
  $SILENT = $silent;
  if $gptfile.defined {
    read-gpt-file($gptfile);
    $gmerge-stypedef = $GPT::FileGenerator::merge-typedef-struct;
    my AllTheThings $att = do-magic($header-file, @gccoptions);
    generate-modules($att);
    return 0;
  }
  if $castxml.defined {
    $CASTXML = True;
    $CASTXML_STD = $castxml if $castxml ne '';
  }
  $gmerge-stypedef =  $merge-stypedef;
  my @files = ();
  my @user-excludes = ();
  if $files {
    for $files.split(',') {
      if $_.starts-with('-') {
        @user-excludes.push($_.substr(1));
      } else {
        @files.push($_);
      }
    }
  }
  if @files !== Empty {
    gpt-note "Displaying content of : " ~ @files.join(', ');
  }
  if @user-excludes !== Empty {
    gpt-note "Excluding content of : " ~ @user-excludes.join(', ');
  }
  
  my AllTheThings $att = do-magic($header-file, @gccoptions);
  gpt-note 'Number of things founds';
  gpt-note '-Types: ' ~ $att.types.elems;
  gpt-note '-Structures: ' ~ $att.structs.elems;
  gpt-note '-Unions: ' ~ $att.unions.elems;
  gpt-note '-Enums: ' ~ $att.enums.elems;
  gpt-note '-Functions: ' ~ $att.functions.elems;
  gpt-note '-Variables: ' ~ $att.variables.elems;
  gpt-note '-Files: ' ~ $att.files.elems;
  gpt-note "Time to generate stuff\n\n";

  if $list-deps.defined {
    list-deps($att, $list-deps);
  }
  if $list-types {
    list-types($att);
  }
  if $list-files {
    list-files($att);
  }
  #if $ooc {
  #  oog-config($ooc);
  #  oog-generate();
  #}
  
  if $define-enum {
    my ($enum-name, $enum-pattern) := $define-enum.split(':');
    my CEnum $e .= new(:name($enum-name), :id(-1));
    for $att.files.kv -> $k, $v {
      if $v.IO.basename ne 'gccxml_builtins.h' and $v.IO.basename !~~ /^std/ {
        my $fh = open $v;
        for $fh.lines -> $line {
          if $line ~~ /^"#"\s*"define" \s+ ($enum-pattern\S*) \s+ (<-[\/]>+)/ {
            my EnumValue $ev .= new;
            $ev.name = $0;
            $e.file-id = $k;
            $ev.init = $1;
            $e.values.push($ev);
          }
        }
      }
    }
    if $e.values.elems !== 0 {
      $att.enums.push($e);
    }    
  }
  # GENERATE STUFF (exclusion are made in sort-by-file)
  dg-init($att);
  if $enums or $all or $define-enum {
    my %h = dg-generate-enums();
    say '## Enumerations';
    my %sortedh = sort-by-file(%h.values);
    for %sortedh.kv -> $k, @v {
      say "\n# == {$att.files{$k}} ==\n";
      for @v -> $ob {
        say $ob<p6str>;
      }
    }
  }
  
  if $structs or $all {
    my %h = dg-generate-structs();
    say '## Structures' ~ "\n";
    my %sortedh = sort-by-file(%h.values);
    for %sortedh.kv -> $k, @v {
      say "\n# == {$att.files{$k}} ==\n";
      for @v.kv -> $i, $ob {
        if $ob<obj> ~~ Struct {
          if @v[$i + 1].defined and @v[$i + 1]<obj> ~~ AnonymousUnion and @v[$i + 1]<obj>.struct.defined {
            say @v[$i + 1]<p6str>;
          }
        }
        if !($ob<obj> ~~ AnonymousUnion and $ob<obj>.struct.defined) {
          say $ob<p6str>;
        }
      }
    }
  }
    
  if $functions or $all {
    say '## Extras stuff' ~ "\n";
    dg-generate-extra();
    my %h = dg-generate-functions();
    say '## Functions' ~ "\n";
    my %sortedh = sort-by-file(%h.values);
    for %sortedh.kv -> $k, @v {
      say "\n# == {$att.files{$k}} ==\n";
      for @v -> $ob {
        say $ob<p6str>;
      }
    }
  }
  
  if $externs or $all {
    say '## Externs' ~ "\n";
    my %h = dg-generate-externs();
    my %sortedh = sort-by-file(%h.values);
    for %sortedh.kv -> $k, @v {
      say "\n# == {$att.files{$k}} ==\n";
      for @v -> $ob {
        say $ob<p6str>;
      }
    }
  }
  
  sub sort-by-file(@array) {
    my %toret;
    for @array -> %s {
      %toret{%s<obj>.file-id}.push(%s) if files-filter(%s<obj>.file-id, $att.files{%s<obj>.file-id}, @user-excludes);
    }
    for %toret.keys -> $k {
      @(%toret{$k}).=sort: {$^a<obj>.start-line > $^b<obj>.start-line};
    }
    return %toret;
  }
}

my constant $PLACEHOLDER = "GPTRIXIE_FIXME";

sub do-magic($header, @other) {
  my %types;
  my @typedefs;
  my %fields;
  my %structs;
  my @cfunctions;
  my @cenums;
  my %cunions;
  my %files;
  my @variables;
  
  my %times;

  my $t = now;
  my $xml-output;
  if $CASTXML {
    my @arg = '--castxml-gccxml', "-std=$CASTXML_STD", '-o', '-', $header, |@other;
    gpt-note "Calling castxml : " ~ @arg.join(' ');
    my $prun = run('castxml', @arg, :out, :err);
    die "Error with castxml : " ~ $prun.err.slurp if $prun.exitcode != 0;
    my $serr = $prun.err.slurp;
    $xml-output = $prun.out.slurp;
    die "Error: no XML produced by castxml " ~ $serr if $xml-output eq "";
  } else {
    gpt-note "Calling GCCXML : $GCC_XML $header -fxml=plop.xml ", |@other;
    $xml-output = run($GCC_XML,  $header, "-fxml=-", @other, :out).out.slurp;
  }
  %times<gccxml> = now - $t;
  $t = now;
  gpt-note "Parsing the XML file";
  my $xml = from-xml($xml-output);
  %times<parse-xml> = now - $t;
  $t = now;
  gpt-note "Doing magic";
  
  for $xml.elements(:TAG<File>) -> $filef {
     %files{$filef<id>} = $filef<name>;
  }

  for $xml.elements() -> $elem {
    given $elem.name {
      # == Types
      when 'FundamentalType' {
        my FundamentalType $t .= new(:id($elem<id>));
        $t.name = $elem<name>;
        %types{$t.id} = $t;
      }
      when 'FunctionType' {
         my FunctionType $t .= new(:id($elem<id>));
         $t.return-type-id = $elem<returns>;
         for $elem.elements(:name<Argument>) -> $arg {
           $t.arguments-type-id.push($arg<type>);
         }
         %types{$t.id} = $t;
      }
      when 'PointerType' {
        my PointerType $t .= new(:id($elem<id>));
        $t.ref-id = $elem<type>;
        $t.ref-type = %types{$t.ref-id} if %types{$t.ref-id}:exists;
        %types{$t.id} = $t;
      }
      when 'CvQualifiedType' {
        my QualifiedType $t .= new(:id($elem<id>));
        $t.ref-id = $elem<type>;
        $t.ref-type = %types{$t.ref-id} if %types{$t.ref-id}:exists;
        %types{$t.id} = $t;
      }
      when 'Typedef' {
        my TypeDefType $t .= new(:id($elem<id>));
        $t.ref-id = $elem<type>;
        $t.set-clocation($elem);
#         say $elem<name>;
#         say $t.ref-id;
        $t.ref-type = %types{$t.ref-id} if %types{$t.ref-id}:exists;
        $t.name = $elem<name>;
        %types{$t.id} = $t;
        @typedefs.push($t);
      }
      when 'ArrayType' {
        my $size = $elem<max>.subst('u', '') ~~ /"0xffffffffffffffff"/ ?? '' !! $elem<max>.subst('u', '') + 1;
        my ArrayType $t .= new(:id($elem<id>), :size($size));
        $t.ref-id = $elem<type>;
        %types{$t.id} = $t;
      }
      when 'ReferenceType' {
        my ReferenceType $t .= new(:id($elem<id>));
        $t.ref-id = $elem<type>;
        $t.ref-type = %types{$t.ref-id} if %types{$t.ref-id}:exists;
        %types{$t.id} = $t;
      }
      # == 'Real' Stuff
      when 'Field' {
        my $pf = Field.new();
        #$pf.set-clocation($elem);
        #$pf.file = %files{$pf.file-id};
        $pf.name = $elem<name>;
        $pf.type-id = $elem<type>;
        %fields{$elem<id>} = $pf;
        %structs{$elem<context>}.fields.push($pf) if %structs{$elem<context>}.defined;
        %cunions{$elem<context>}.members.push($pf) if %cunions{$elem<context>}.defined;
      }
      when 'Struct' {
        my $s = Struct.new;
        $s.name = $elem<name>.defined ?? $elem<name> !! $elem<mangled>;
        $s.name = $PLACEHOLDER if !$s.name.defined || $s.name eq '';
        $s.id = $elem<id>;
        #say "Struct : ", $s.id ~ $s.name;
        $s.set-clocation($elem);
        $s.file = %files{$s.file-id};
        %structs{$s.id} = $s;
        my StructType $t .= new(:id($s.id), :name($s.name));
        $t.set-clocation($elem);
        %types{$t.id} = $t;
      }
      when 'Class' { #FIXME need to add real stuff around that
        my ClassType $c .= new(:id($elem<id>), :name($elem<name>));
        %types{$c.id} = $c;
      }
      when 'Union' {
        my UnionType $t .= new(:id($elem<id>));
        %types{$t.id} = $t;
        $t.set-clocation($elem);
        my $u;
        if $elem<name>.defined and $elem<name> ne "" {
          $u = CUnion.new(:id($elem<id>));
          $u.name = $elem<name>;
        } else {
          $u = AnonymousUnion.new(:id($elem<id>));
          $u.struct = %structs{$elem<context>};
        }
        say $t.id, $u.WHAT;
        $u.set-clocation($elem);
        %cunions{$u.id} = $u;
      }
      when 'Enumeration' {
        my CEnum $enum .= new(:id($elem<id>), :name($elem<name>));
        my EnumType $t .= new(:id($elem<id>), :name($elem<name>));
        %types{$t.id} = $t;
        $enum.set-clocation($elem);
        $enum.file = %files{$enum.file-id};
        for @($elem.elements()) -> $enumv {
          my EnumValue $nv .= new(:name($enumv.attribs<name>), :init($enumv.attribs<init>));
          $enum.values.push($nv);
        }
        @cenums.push($enum);
      }
      when 'Function' {
        next if $elem<name> ~~ /^__/;
        my Function $f .= new(:name($elem<name>), :id($elem<id>));
        $f.returns-id = $elem<returns>;
        $f.set-clocation($elem);
        $f.file = %files{$f.file-id};
        for @($elem.elements()) -> $param {
          next if $param.name ne 'Argument';
          my FunctionArgument $a .= new(:name($param.attribs<name>));
          $a.set-clocation($param);
          $a.file = %files{$a.file-id};
          $a.type-id = $param<type>;
          $f.arguments.push($a);
        }
        @cfunctions.push($f)
      }
      when 'Variable' {
        if $elem<extern>.defined and $elem<extern> == 1 {
          my ExternVariable $e .= new(:id($elem<id>), :name($elem<name>));
          $e.type-id = $elem<type>;
          $e.set-clocation($elem);
          #$e.file = %files($e.file-id);
          @variables.push($e);
        }
      }
    }
  }
  

  #We probably can resolve every type now.
  sub resolvetype {
      my $change = True; #Do something like bubble sort, until we solve everytype, let's boucle
      while ($change) {
          $change = False;
          for %types.kv -> $id, $t {
              if $t ~~ IndirectType {
                  unless $t.ref-type:defined {
                      #say "Found an undef indirect id: "~ $t.ref-id;
                      $t.ref-type = %types{$t.ref-id};
                      $change = True;
                  }
                  CATCH {
                      default {
                          say $t.raku;
                      }
                  }
              }
          }
      }
  }
    resolvetype();

  sub merge-stypedef {
    for %types.kv -> $id, $t {
      if $t ~~ TypeDefType and $t.ref-type ~~ StructType {
        %types{$id} = $t.ref-type;
        $t.ref-type.name = $t.name;
        %structs{$t.ref-id}.name = $t.name;
      }
    }
  }
  #Handle functionType
  for %types.kv -> $k, $v {
    if $v ~~ FunctionType {
      $v.return-type = %types{$v.return-type-id};
      for $v.arguments-type-id -> $id {
        $v.arguments-type.push(%types{$id});
      }
    }
  }
  
  merge-stypedef() if $gmerge-stypedef;
  for @cfunctions -> $f {
    $f.returns = %types{$f.returns-id};
    for $f.arguments -> $a {
      $a.type = %types{$a.type-id};
    }
  }
  for %fields.kv ->  $id, $f {
    $f.type = %types{$f.type-id};
    if $f.type ~~ UnionType {
      %cunions{$f.type.id}.field = $f if %cunions{$f.type.id} ~~ AnonymousUnion;
    }
  }
  for %cunions.kv -> $k, $cu {
    if $cu ~~ AnonymousUnion {
      $cu.gen-name = $cu.struct.name ~ "_" ~ $cu.field.name ~ "_Union";
    }
  }
  for @variables -> $v {
    $v.type = %types{$v.type-id};
    #say $v.name ~ ' - ' ~ $v.type;
  }
  say "Before FIX";
  for %structs.kv -> $id, $s {
      say "ID : ", $id, " name = ", $s.name
  }

  sub fix-struct-name { # CASTXML does not give a name to struct defined like typedef struct {} name
      # Also does not give nice name to anonymous struct in union
      #say "fix stuff";
      for %structs.keys -> $id {
          next if %structs{$id} !~~ Struct;
#          say "Id: ", $id, "name", %structs{$id}.name;
          if %structs{$id}.name eq $PLACEHOLDER {
              # Merging typedef struct {}
              for @typedefs -> $td {
                  if $td.ref-id eq $id {
 #                     say "merging struct ", $id , " with typedef ", $td.id;
                      %structs{$id}.name = $td.name;
                      %structs{$id}.id = $td.id;
                      %types{$id}.id = $td.id;
                      %types{$id}.name = $td.name;
                      %types{$td.id} = %types{$id};
                      %types{$id}:delete;
                      %structs{$td.id} = %structs{$id};
                      %structs{$id}:delete;
                      @typedefs.splice(@typedefs.first($td, :k), 1);
                      last;
                  }
              }
          }
      }
      # anonym union
      for %cunions.kv -> $id, $union {
          #say "Union : " ~ $union.id;
          for $union.members -> $field {
              #say "Field : " ~ $field.name ~ $field.type.id;
              if $field.type ~~ StructType {
                  #say "Find struct type in " ~ $union.name;
                  if %structs{$field.type.id}.name eq $PLACEHOLDER {
                      if $union !~~ AnonymousUnion {
                          %structs{$field.type.id}.name =
                                  $union.name ~ "_anonymousStruct" ~ $field.type.id;
                          %types{$field.type.id} = %structs{$field.type.id}.name;
                      } else {
                          %structs{$field.type.id}.name =
                                  "anonymousUnion{$id}_anonymousStruct" ~ $field.type.id;
                          %types{$field.type.id} = %structs{$field.type.id}.name;
                      }
                  }
              }
          }
      }
  }
  fix-struct-name();
  #say $_.id, " : ", $_.name  for %structs.values;
  #exit 1;
  %times<magic> = now - $t;
  gpt-note "Times -- gccxml: %times<gccxml> sec; xml parsing: %times<parse-xml> sec; magic: %times<magic>";
   my $att = AllTheThings.new;
   $att.files = %files;
   $att.types = %types;
   $att.functions = @cfunctions;
   $att.enums = @cenums;
   $att.structs = %structs;
   $att.unions = %cunions;
   $att.variables = @variables;
   return $att;
}

# say "\n==CSTRUCT==";
# for %struct.kv -> $k, $v {
#   say "-$k : {$v.name}";
#   for $v.fields -> $f {
#     say "   {$f.type.Str} ({$f.type-id})  '{$f.name}'";
#   }
# }
# 
# say "==FUNCTIONS==";
# 
# for @cfunctions -> $f {
#   my @tmp;
#   for $f.arguments -> $a {
#     @tmp.push($a.type ~ ' ' ~ $a.name);
#   }
#   say $f.returns ~ "\t\t" ~ $f.name ~ '(' ~ @tmp.join(', ') ~ ')';
# }

